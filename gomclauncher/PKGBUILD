# Maintainer: Mikakutei <mikakutei@outlook.com>
# Contributor: Coelacanthus <coelacanthus@outlook.com>

pkgname=gomclauncher
pkgver=1.4.8
pkgrel=1
pkgdesc="A Minecraft Launcher"
arch=('x86_64')
url="https://github.com/xmdhs/${pkgname}"
license=('MIT')
depends=('glibc')
makedepends=('go')
conflicts=('gomclauncher-bin')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "https://raw.githubusercontent.com/xmdhs/${pkgname}/v${pkgver}/LICENSE")
sha256sums=('5639b6f308632d5aea01802b0778ca9cd94ea04ecbabb012527f19b0d2a89cf8'
            '9c9c42c8f4534fe4e44fb26b1e3df7b133fbafbb2f72563653d4f33ecf948fa9')

prepare() {
    cd "${pkgname}-${pkgver}" 
    mkdir -p build/
}

build() {
    cd "${pkgname}-${pkgver}"    
    go build \
       -trimpath \
       -buildmode=pie \
       -mod=readonly \
       -modcacherw \
       -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
       -o build \
       .
}

package() {
    cd "${pkgname}-${pkgver}"
    install -Dm755 build/${pkgname} "${pkgdir}/usr/bin/gml"
    install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

